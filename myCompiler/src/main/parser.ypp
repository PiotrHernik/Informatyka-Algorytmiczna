%require "3.2"
%language "c++"

%define api.parser.class {Parser}
%define api.value.type variant
%define api.token.raw
%define api.token.constructor

%define parse.assert
%define parse.trace
%define parse.error detailed
%define parse.lac full

%locations

%param {yy::Scanner &scanner}

%code requires
{

#include "Location.hpp"
#include <Identifier.hpp>
#include <IdentifierPid.hpp>
#include <IdentifierTPid.hpp>
#include <IdentifierTNum.hpp>
#include "Value.hpp"
#include "ValueId.hpp"
#include "ValueNum.hpp"
#include "Expression.hpp"
#include "ExpressionDiv.hpp"
#include "ExpressionMinus.hpp"
#include "ExpressionPlus.hpp"
#include "ExpressionMult.hpp"
#include "ExpressionMod.hpp"
#include "ArgDeclaration.hpp"
#include "Args.hpp"
#include "Ast.hpp"
#include "Command.hpp"
#include "CommandAssign.hpp"
#include "CommandDownTo.hpp"
#include "CommandForTo.hpp"
#include "CommandIf.hpp"
#include "CommandIfElse.hpp"
#include "CommandRead.hpp"
#include "CommandWrite.hpp"
#include "CommandRepeat.hpp"
#include "CommandProcCall.hpp"
#include "CommandWhile.hpp"
#include "Condition.hpp"
#include "ConditionEq.hpp"
#include "ConditionNEq.hpp"
#include "ConditionGEq.hpp"
#include "ConditionGt.hpp"
#include "ConditionLEq.hpp"
#include "ConditionLt.hpp"
#include "declarations/Declaration.hpp"
#include "procCall/ProcCall.hpp"
#include "Procedure.hpp"
#include "ProcHead.hpp"
#include "Program.hpp"
#include "ProgramWithDeclarations.hpp"
#include "ProgramWithoutDeclarations.hpp"
#include "Enums.hpp"

#include "makeArgs.hpp"
#include "makeArgsDeclaration.hpp"
#include "makeDeclarations.hpp"
#include "makeProcedures.hpp"
#include "makeCommands.hpp"

#include <string>
#include <memory>
#include <iostream>

namespace yy {
    class Scanner;
}
}

%code {
#include "scanner.hpp"


yy::Parser::symbol_type yylex(yy::Scanner &scanner) {
    return scanner.scan();
}
}

%token KW_IF 
%token KW_ELSE
%token NEWLINE
%token KW_PROCEDURE 
%token KW_IS 
%token KW_BEGIN 
%token KW_END
%token KW_PROGRAM
%token KW_THEN
%token KW_ENDIF
%token KW_WHILE 
%token KW_DO
%token KW_ENDWHILE
%token KW_REPEAT
%token KW_UNTIL
%token KW_FOR
%token KW_FROM
%token KW_ENDFOR
%token KW_DOWNTO
%token KW_READ
%token KW_WRITE
%token KW_TO
%token KW_T

%token EQUAL
%token NEQUAL
%token LESS
%token MORE
%token LESSOREQUAL
%token MOREOREQUAL

%token ASSIGN

%token SEMICOLON
%token COLON
%token COMMA
%token LPRNT
%token RPRNT
%token LBRACKET
%token RBRACKET

%nonassoc UMINUS
%left PLUS MINUS
%left MULT DIV MOD
  
%token <long long> NUM
%token <std::string> PIDENTIFIER

%type <std::shared_ptr<Identifier>> identifier
%type <std::shared_ptr<Value>> value
%type <std::shared_ptr<Condition>> condition
%type <std::shared_ptr<Expression>> expression
%type <std::vector<std::shared_ptr<Args>>> args
%type <std::vector<std::shared_ptr<ArgDeclaration>>> args_decl
%type <std::vector<std::shared_ptr<Declaration>>> declarations
%type <std::shared_ptr<ProcHead>> proc_head
%type <std::shared_ptr<ProcCall>> proc_call
%type <std::shared_ptr<Command>> command
%type <std::vector<std::shared_ptr<Command>>> commands
%type <std::vector<std::shared_ptr<Procedure>>> procedures
%type <std::shared_ptr<Program>> main
%type <long long> number

%%
program_all: 
        procedures main { mainProgram($1, std::move($2)); }

procedures: 
        procedures KW_PROCEDURE proc_head KW_IS declarations KW_BEGIN commands KW_END { $$ = makeProcedures($1, std::move($3), $5, $7); }
        | procedures KW_PROCEDURE proc_head KW_IS KW_BEGIN commands KW_END { $$ = makeProcedures($1, std::move($3), $6); }
        | { $$ = makeProcedures(); }


main: 
        KW_PROGRAM KW_IS declarations KW_BEGIN commands KW_END { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ProgramWithDeclarations>($3, $5, location); }
        | KW_PROGRAM KW_IS KW_BEGIN commands KW_END { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ProgramWithoutDeclarations>($4, location); }

commands:
        commands command { $$ = makeCommands($1, std::move($2));  }
        | command { $$ = makeCommands(std::move($1)); }

command:
        identifier ASSIGN expression SEMICOLON { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandAssign>(std::move($1), std::move($3), location); }
        | KW_IF condition KW_THEN commands KW_ENDIF { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandIf>(std::move($2), $4, location); }
        | KW_IF condition KW_THEN commands KW_ELSE commands KW_ENDIF { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandIfElse>(std::move($2), $4, $6, location); }
        | KW_WHILE condition KW_DO commands KW_ENDWHILE { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandWhile>(std::move($2), $4, location); }
        | KW_REPEAT commands KW_UNTIL condition SEMICOLON { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandRepeat>(std::move($4), $2, location); }
        | KW_FOR PIDENTIFIER KW_FROM value KW_TO value KW_DO commands KW_ENDFOR { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandForTo>($2, std::move($4), std::move($6), $8, location); } 
        | KW_FOR PIDENTIFIER KW_FROM value KW_DOWNTO value KW_DO commands KW_ENDFOR { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandDownTo>($2, std::move($4), std::move($6), $8, location); }
        | proc_call SEMICOLON { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandProcCall>(std::move($1), location); }
        | KW_READ identifier SEMICOLON { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandRead>(std::move($2), location); }
        | KW_WRITE value SEMICOLON { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<CommandWrite>(std::move($2), location); }

proc_head:
        PIDENTIFIER LPRNT args_decl RPRNT { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ProcHead>($1, $3, location); }

proc_call:
        PIDENTIFIER LPRNT args RPRNT { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ProcCall>($1, $3, location); }

declarations:
        declarations COMMA PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeDeclaration($1, $3, {}, location); }
        | declarations COMMA PIDENTIFIER LBRACKET number COLON number RBRACKET { Location location(@1.begin.line, @1.begin.column); $$ = makeDeclaration($1, $3, {$5, $7}, location); }
        | PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeDeclaration($1, {}, location); }
        | PIDENTIFIER LBRACKET number COLON number RBRACKET { Location location(@1.begin.line, @1.begin.column); $$ = makeDeclaration($1, {$3, $5}, location); }

args_decl:
        args_decl COMMA PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgsDeclaration($1, $3, DeclarationEnum::Pid, location); }
        | args_decl COMMA KW_T PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgsDeclaration($1, $4, DeclarationEnum::Table, location); }
        | PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgsDeclaration($1, DeclarationEnum::Pid, location); }
        | KW_T PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgsDeclaration($2, DeclarationEnum::Table, location); }

args:
        args COMMA PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgs($1, $3, location); }
        | PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = makeArgs($1, location); }

expression:
        value { $$ = std::make_shared<Expression>(std::move($1)); }
        | value PLUS value { $$ = std::make_shared<ExpressionPlus>(std::move($1), std::move($3)); }
        | value MINUS value { $$ = std::make_shared<ExpressionMinus>(std::move($1), std::move($3)); }
        | value MULT value { $$ = std::make_shared<ExpressionMult>(std::move($1), std::move($3)); }
        | value DIV value { $$ = std::make_shared<ExpressionDiv>(std::move($1), std::move($3)); }
        | value MOD value { $$ = std::make_shared<ExpressionMod>(std::move($1), std::move($3)); }

condition:
        value EQUAL value { $$ = std::make_shared<ConditionEq>(std::move($1), std::move($3)); }
        | value NEQUAL value { $$ = std::make_shared<ConditionNEq>(std::move($1), std::move($3)); }
        | value MORE value { $$ = std::make_shared<ConditionGt>(std::move($1), std::move($3)); }
        | value LESS value { $$ = std::make_shared<ConditionLt>(std::move($1), std::move($3)); }
        | value MOREOREQUAL value { $$ = std::make_shared<ConditionGEq>(std::move($1), std::move($3)); }
        | value LESSOREQUAL value { $$ = std::make_shared<ConditionLEq>(std::move($1), std::move($3)); }

value:
        number { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ValueNum>($1, location); }
        | identifier { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<ValueId>(std::move($1), location); }

number:
        NUM { $$ = $1; }
        | MINUS NUM %prec UMINUS { $$ = -$2; }

identifier:
        PIDENTIFIER { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<IdentifierPid>($1, location); }
        | PIDENTIFIER LBRACKET number RBRACKET { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<IdentifierTNum>($1, $3, location); }
        | PIDENTIFIER LBRACKET PIDENTIFIER RBRACKET { Location location(@1.begin.line, @1.begin.column); $$ = std::make_shared<IdentifierTPid>($1, $3, location); }


%%

void yy::Parser::error(const location_type& loc, const std::string& msg)
{
    std::cout << "ERROR at "<< loc << ", message: " << msg << std::endl;
}
